// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Check if it's an A-instruction (bit 15 = 0) or C-instruction (bit 15 = 1)
    Not(in=instruction[15], out=isAInst);
    
    // Load A register if it's an A-instruction OR if instruction wants to store in A
    Or(a=isAInst, b=instruction[5], out=loadA);
    
    // Pick what goes into A: address from instruction, or ALU result
    Mux16(a=instruction, b=ALUout, sel=instruction[15], out=newA);
    
    // A register stores addresses (lower 15 bits become memory address)
    ARegister(in=newA, load=loadA, out=prevA, out[0..14]=addressM);
    
    // D register stores data - only load on C-instructions that want to store in D
    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=ALUout, load=loadD, out=prevD);
    
    // Choose ALU input: A register or memory value M (bit 12 decides)
    Mux16(a=prevA, b=inM, sel=instruction[12], out=amchosen);

    // ALU does the math - bits 11-6 control what operation
    And16(a=false, b=false, out=ALUoutA);
    ALU(x=prevD, y=amchosen, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], 
        out=ALUoutC, zr=outzr, ng=outng);
    
    // Use ALU result for C-instructions, 0 for A-instructions
    Mux16(a=ALUoutC, b=ALUoutA, sel=isAInst, out=ALUout);

    // Jump logic: check if ALU result is positive, zero, or negative
    Not(in=outzr, out=nzr);
    Not(in=outng, out=nng);
    And(a=nng, b=nzr, out=pos);
    
    // Check jump conditions (bits 0,1,2)
    And(a=pos, b=instruction[0], out=jgt);      // jump if > 0
    And(a=outzr, b=instruction[1], out=jeq);     // jump if = 0
    And(a=outng, b=instruction[2], out=jlt);    // jump if < 0
    
    // Combine jump conditions
    Or(a=jgt, b=jeq, out=jgtOrjeq);
    Or(a=jgtOrjeq, b=jlt, out=jumpcondRaw);
    And(a=instruction[15], b=jumpcondRaw, out=jumpcond);
    
    // Program counter - normally just counts up, jumps if condition met
    PC(in=prevA, load=jumpcond, inc=true, reset=reset, out[0..14]=pc);

    // Write to memory if it's a C-instruction that wants to store in M
    And(a=instruction[15], b=instruction[3], out=writeM);
    
    // Always output what ALU computed (will be written if writeM is true)
    And16(a=ALUout, b=ALUout, out=outM);
}